# Eddie Murphy - I want half
# https://www.youtube.com/watch?v=Q4YJHvzo2io
# by:
# - Yves Candel
# - Nick Savers
# - Joris Bontje

# Parties
#- Partner 1
#- Partner 2

# State
#0 - Start
#1 - Married
#2 - Divorced

# Storage
# 1000 - State
# 1001 - Partner 1
# 1002 - Partner 2

I_STATE = 1000
I_PARTNER_1 = 1001
I_PARTNER_2 = 1002
I_WITHDRAW_TO = 1003
I_WITHDRAW_AMOUNT = 1004
I_WITHDRAW_CREATOR = 1005
I_DIVORCE_CREATOR = 1006

S_START = 0
S_PROPOSED = 1
S_MARRIED = 2
S_DIVORCED = 3

TX_WITHDRAW = 1
TX_DIVORCE = 2

if tx.value < 100 * block.basefee:
    stop

state = contract.storage[I_STATE]
if state == S_START:
    contract.storage[I_PARTNER_1] = tx.sender
    contract.storage[I_PARTNER_2] = tx.data[0]
    contract.storage[I_STATE] = S_PROPOSED
else:
    partner_1 = contract.storage[I_PARTNER_1]
    partner_2 = contract.storage[I_PARTNER_2]

    if state == S_PROPOSED and tx.sender == partner_2 and tx.data[0] == partner_1:
        contract.storage[I_STATE] = S_MARRIED

    else if state == S_MARRIED and tx.sender == partner_1 or tx.sender == partner_2:
        if tx.data[0] == TX_WITHDRAW:
            creator = contract.storage[I_WITHDRAW_CREATOR]
            if creator != 0 and contract.storage[I_WITHDRAW_TO] == tx.data[1] and contract.storage[I_WITHDRAW_AMOUNT] == tx.data[2] and creator != tx.sender:
                mktx(tx.data[1], tx.data[2], 0, 0)
                contract.storage[I_WITHDRAW_TO] = 0
                contract.storage[I_WITHDRAW_AMOUNT] = 0
                contract.storage[I_WITHDRAW_CREATOR] = 0
            else:
                contract.storage[I_WITHDRAW_TO] = tx.data[1]
                contract.storage[I_WITHDRAW_AMOUNT] = tx.data[2]
                contract.storage[I_WITHDRAW_CREATOR] = tx.sender

        else if tx.data[0] == TX_DIVORCE:
            creator = contract.storage[I_DIVORCE_CREATOR]
            if creator != 0 and creator != tx.sender:
                balance = block.account_balance(contract.address)
                mktx(partner_1, balance / 2, 0, 0)
                mktx(partner_2, balance / 2, 0, 0)
                contract.storage[I_STATE] = S_DIVORCED
            else:
                contract.storage[I_DIVORCE_CREATOR] = tx.sender
